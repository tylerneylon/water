# Start work on javascript

>:
  run.run("symbols = {}\n")
  run.run("def log_fn(str_obj): print(str_obj['value'])\n")
  run.run("log_obj = {'type': 'fn', 'value': log_fn}\n")
  run.run("symbols['console'] = {'type': 'obj', 'value': {'log': log_obj}}\n")
  run.run("def make_locals():\n  lo = {}\n  lo.update(symbols)\n  return lo\n")

# TODO Figure out how to allow this fn to be callable from add_code blocks.
>:
  def push_value(rule):
    run.add(rule)
    run.add("['value']")

>:
  env.symbols = {}
  def log_fn(s): print(s)
  log = {'type': 'fn', 'value': log_fn, 'num_params': 1}
  console = {'type': 'obj', 'value': {'log': log}}
  env.symbols['console'] = console
  mode.symbols = env.symbols
  env.next_sym_num = 0
  def new_sym():
    sym = 'sym_%d' % env.next_sym_num
    env.next_sym_num += 1
    return sym
  env.new_sym = new_sym

>
  s ->
    "\s*"
  statement ->
    s stmnt
  stmnt ->
    reg_stmnt
    : run.run(reg_stmnt)
  reg_stmnt ->
    stmnt_no_smcln s "(;)?"
    add_code: run.add(stmnt_no_smcln)
  stmnt_no_smcln -> func_def | assign | while | expr
  func_def ->
    'function' s ident s '(' s params s ')' s '{' s -|
    start:
      parse.push_mode('block')
      mode.block_stmnts = []
    add_code:
      src_names, code_names = params.names()
      run.add("def fn(%s):\n" % ','.join(code_names))
      run.push_indent('  ')
      run.add("symbols = make_locals()\n")
      for i in range(len(src_names)):
        run.add("symbols['%s'] = %s\n" % (src_names[i], code_names[i]))
      for r in mode_result: run.add(r)
      run.pop_indent()
      run.add("symbols['%s'] = {'type': 'fn', 'value': fn}\n" % ident.src())
  assign -> direct_assign | inc_assign
  direct_assign ->
    "(var)?" s ident s '=' s expr
    add_code:
      run.add("symbols['%s'] = " % ident.src())
      run.add(expr, "\n")
  inc_assign ->
    ident s inc_op
    add_code:
      run.add(ident, "['value'] ", inc_op, "\n")
  while ->
    'while' s '(' s cond s ')' s '{' -|
    start:
      parse.push_mode('block')
      mode.block_stmnts = []
    add_code:
      run.add("while ", cond, ":\n")
      run.push_indent('  ')
      for r in mode_result: run.add(r)
      run.pop_indent()
  call ->
    ident s '(' s args s ')'
    add_code:
      run.add("symbols['%s']['value'](" % ident.src())
      run.add(args)
      run.add(")\n")
  ident ->
    "[A-Za-z_]\w*"
    names: return [self.src()], ['param0']
    add_code:
      run.add("symbols['%s']" % self.src())
  params -> multi_params | ident
  multi_params ->
    ident s ',' s params
    src_names: return [ident.src()] + params.src_names()
    names:
      src = self.src_names()
      return src, ['param%d' % i for i in range(len(src))]
  expr -> obj_elem | call | var | js_str | num
  inc_op ->
    '--'
    add_code: run.add("-= 1")
  cond ->
    expr s cond_op s expr
    add_code:
      run.add(expr[0], "['value']")
      run.add(' %s ' % cond_op.src())
      run.add(expr[1], "['value']")
  args -> multi_args | expr
  multi_args ->
    expr s ',' s args
  obj_elem ->
    var '.' ident
    add_code:
      run.add("symbols['%s']['value']['%s']" % (var, ident.src()))
  var ->
    ident
    add_code: run.add("symbols['%s']" % ident.src())
  js_str ->
    "['\"]" -|
    start:
      parse.push_mode('js_str', {'endchar': tokens[0][0]})
      mode.chars = [mode.endchar]
    add_code: run.add("{'type': 'str', 'value': %s}" % self.src())
    src: return mode_result
  num ->
    "[0-9]+"
    add_code: run.add("{'type': 'num', 'value': %s}" % self.src())
  cond_op ->
    '>'

# So far this is for the body fo a func_def or while.
> block
  stmnt -> close_brace | func_stmnt
  close_brace ->
    '}'
    : parse.pop_mode(mode.block_stmnts)
  func_stmnt ->
    reg_stmnt
    : mode.block_stmnts.append(reg_stmnt)

> js_str
  phrase -> escape_seq | char
  escape_seq ->
    "\\\\(.)"
    :
      if tokens[0][1] != mode.endchar:
        mode.chars.append("\\")
      mode.chars.append(tokens[0][1])
  char ->
    "."
    :
      c = tokens[0][0]
      mode.chars.append(c)
      if c == mode.endchar: parse.pop_mode(''.join(mode.chars))

function f(n) {
  var p = console.log;
  while (n > 0) {
    p('hi');
    n--;
  }
}
f(3);
